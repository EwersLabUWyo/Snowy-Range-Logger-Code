'Program: CP Eddy-Covariance (Fire)
'Written by: John Frank, edited by Mario Bretfeld
'Last edit: 2018-08-22


'*** Wiring ***

'SDM INPUT
'C1				LI-7500 SDM Data (gray)
'C2				LI-7500 SDM Clock (blue)
'C3				LI-7500 SDM Enable (brown)
'G				LI-7500 SDM reference (black)
'				LI-7500 SDM shield (white)

'CONTROL PORTS (RS-232 SERIAL)

'POWER IN
'12V				datalogger (red)
'G				datalogger (black)

'EXTERNAL POWER SUPPLY
'POS		
'				LI-7500 power (red with white)
'				datalogger (red)
'NEG				
'				LI-7500 power reference (red with black)
'				LI-7500 ground (green)
'				datalogger (black)


PipeLineMode

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'Constants
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
Const const_M1 = 1411810000  'Unique
Const const_M2 = 7292450     'Unique
Const const_M3 = 69465       'Unique
Const const_B1 = 5455940     'Unique
Const const_B2 = 130581      'Unique
Const const_B3 = 2428.18     'Unique

Const pyrano_upper = 14.42      'Unique up pyranometer sensitivity from CNR4 cal sheet
Const pyrano_lower = 12.58      'Unique down pyranometer sensitivity from CNR4 cal sheet
Const pyrgeo_upper = 14.15      'Unique up pyrgeometer sensitivity from CNR4 cal sheet
Const pyrgeo_lower = 13.54      'Unique down pyrgeometer sensitviity from CNR4 cal sheet

Const LI190_Cal_1 = 6.57     'Unique calibration factor for LI190SB #1 calibration sheet
Const LI190_Cal_2 = 6.65     'Unique calibration factor for LI190SB #2 calibration sheet

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'Variable Definitions
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'Datalogger Diagnostics
Public DIAG_CR3000(2)
Alias DIAG_CR3000(1) = T_CR3000 : Units T_CR3000 = C
Alias DIAG_CR3000(2) = V_CR3000 : Units V_CR3000 = V
Dim n = 1 : Units n = samples       'Number of samples in the online stats.

'CSAT3 Sonic Anemometer - Within canopy
Public Sonic_CSAT3B(5)
Alias Sonic_CSAT3B(1) = Ux_CSAT3B : Units Ux_CSAT3B = m/s
Alias Sonic_CSAT3B(2) = Uy_CSAT3B : Units Uy_CSAT3B = m/s
Alias Sonic_CSAT3B(3) = Uz_CSAT3B : Units Uz_CSAT3B = m/s
Alias Sonic_CSAT3B(4) = Ts_CSAT3B : Units Ts_CSAT3B = C
Alias Sonic_CSAT3B(5) = DIAG_CSAT3B : Units DIAG_CSAT3B = unitless

'LI-7500 CO2/H2O IRGA
Public LI7500(4)
Alias LI7500(1) = rho_c_LI7500 : Units rho_c_LI7500 = mg/m^3
Alias LI7500(2) = rho_v_LI7500 : Units rho_v_LI7500 = g/m^3
Alias LI7500(3) = P_LI7500 : Units P_LI7500 = kPa
Alias LI7500(4) = DIAG_LI7500 : Units DIAG_LI7500 = unitless

'CNR4 NetRad
Public CNR4(5)
Alias CNR4(1) = SWD : Units SWD = W/m^2
Alias CNR4(2) = SWU : Units SWU = W/m^2
Alias CNR4(3) = LWD : Units LWD = W/m^2
Alias CNR4(4) = LWU : Units LWU = W/m^2
'Alias CNR4(5) = Ta
Alias CNR4(5) = Tb : Units Tb = C
Public albedo : Units albedo = W/m^2
Public Rn : Units Rn = W/m^2
Dim CNR4_raw(5) 'raw data 

'LI-190SB PAR
Public PAR(2) : Units PAR = = umol/s/m^2
Alias PAR(1) = PAR_up                        'flux density for LI190SB facing down (measuring upwards PAR)
Alias PAR(2) = PAR_dn                        'flux density for LI190SB facing up (measuring downwards PAR)
Dim PAR_raw(2) 'raw data 

'HMP & RTD for RH/AirT
Public AirT_2m : Units AirT_2m = C
Public AirT_4m : Units AirT_4m = C
Public AirT_6m : Units AirT_6m = C
Public RH_6m : Units RH_6m = %
Dim AirT_raw(3)

'RM Young for Wind Speed/Wind Direction
Public WS(2) : Units WS = m/s 
Public WD(2) : Units WD = degrees
Alias WS(1) = WS_2m
Alias WS(2) = WS_4m
Alias WD(1) = WD_2m
Alias WD(2) = WD_4m

'SB 111 Surface Temp
Dim m                               'slope for target T calculation
Dim b                               'offset for target T calculation
Dim TT_K                            'SB111 Target Temp (K)
Dim SBT_K                           'SB111 Block Temp (K)
Public SurfT : Units SurfT = Deg C                 'SI111 IR Target Temp (C)
Public SBT_C : Units SBT_C = Deg C               'SI111 Block Temp (C)
Public TTmV : Units TTmV = mV                    'SI111 Target raw

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'Output Definitions
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'Time Series Flux Data
DataTable (TimeSeriesData,TRUE,-1)
	DataInterval (0,100,mSec,100)
  TableFile ("CRD:"&Status.SerialNumber(1,1)&".CP_ECF_10Hz",64,-1,0,30,Min,0,0)
	Sample (1,Ux_CSAT3B,IEEE4)
	Sample (1,Uy_CSAT3B,IEEE4)
	Sample (1,Uz_CSAT3B,IEEE4)
	Sample (1,Ts_CSAT3B,IEEE4)
	Sample (1,rho_c_LI7500,IEEE4)
	Sample (1,rho_v_LI7500,IEEE4)
  Sample (1,DIAG_CSAT3B,IEEE4)
  Sample (1,DIAG_LI7500,IEEE4)
EndTable

'Summary Flux Data
DataTable (Flux30Min,TRUE,-1)
	DataInterval (0,30,Min,100)
  TableFile ("CRD:"&Status.SerialNumber(1,1)&".CP_ECF_Flux30Min",64,-1,0,1,Day,0,0)
  Totalize (1,n,IEEE4,0)
	Average (1,Ux_CSAT3B,IEEE4,0)
	Average (1,Uy_CSAT3B,IEEE4,0)
	Average (1,Uz_CSAT3B,IEEE4,0)
	Average (1,Ts_CSAT3B,IEEE4,0)
	Average (1,rho_c_LI7500,IEEE4,0)
	Average (1,rho_v_LI7500,IEEE4,0)
	Average (1,P_LI7500,IEEE4,0)
	Average (1,DIAG_CSAT3B,IEEE4,0)
	Average (1,DIAG_LI7500,IEEE4,0)
  Average (1,T_CR3000,IEEE4,0)
  Average (1,V_CR3000,IEEE4,0)
	StdDev (1,Ux_CSAT3B,IEEE4,False)
	StdDev (1,Uy_CSAT3B,IEEE4,False)
	StdDev (1,Uz_CSAT3B,IEEE4,False)
	StdDev (1,Ts_CSAT3B,IEEE4,False)
	StdDev (1,rho_c_LI7500,IEEE4,False)
	StdDev (1,rho_v_LI7500,IEEE4,False)
	StdDev (1,P_LI7500,IEEE4,False)
	StdDev (1,DIAG_CSAT3B,IEEE4,False)
	StdDev (1,DIAG_LI7500,IEEE4,False)
	StdDev (1,T_CR3000,IEEE4,False)
	StdDev (1,V_CR3000,IEEE4,False)
EndTable

'Summary Met Data
DataTable (Met30Min,TRUE,-1)
	DataInterval (0,30,Min,100)
  TableFile ("CRD:"&Status.SerialNumber(1,1)&".CP_ECF_Met30Min",64,-1,0,1,Day,0,0)
  
  Average (1, AirT_2m,IEEE4,False)
  Minimum (1, AirT_2m,IEEE4,False,False)
  Maximum (1, AirT_2m,IEEE4,False,False)
  StdDev (1, AirT_2m,IEEE4,False)
  
  Average (1, AirT_4m,IEEE4,False)
  Minimum (1, AirT_4m,IEEE4,False,False)
  Maximum (1, AirT_4m,IEEE4,False,False)
  StdDev (1, AirT_4m,IEEE4,False)
  
  Average (1, AirT_6m,IEEE4,False)
  Minimum (1, AirT_6m,IEEE4,False,False)
  Maximum (1, AirT_6m,IEEE4,False,False)
  StdDev (1, AirT_6m,IEEE4,False)
  
  Sample  (1, RH_6m,IEEE4)
  Minimum (1, RH_6m,IEEE4,False,False)
  Maximum (1, RH_6m,IEEE4,False,False)
  StdDev (1, RH_6m,IEEE4,False)

	Average (1, SurfT,IEEE4,False)
	Minimum (1, SurfT,IEEE4,False,False)
	Maximum (1, SurfT,IEEE4,False,False)
	StdDev (1, SurfT,IEEE4,False)
	
	Average (1, SBT_C,IEEE4,False)
	Minimum (1, SBT_C,IEEE4,False,False)
 	Maximum (1, SBT_C,IEEE4,False,False)
 	StdDev (1, SBT_C,IEEE4,False)
	
  Average (5, CNR4,IEEE4,False)
	StdDev (5, CNR4,IEEE4,False)
  Average (1, albedo,IEEE4,False)
	StdDev (1, albedo,IEEE4,False)
  Average (1, Rn,IEEE4,False)
  StdDev (1, Rn,IEEE4,False)

  Average (1, PAR_up,IEEE4,False)
  StdDev (1, PAR_up,IEEE4,False)  
  Average (1, PAR_dn,IEEE4,False)  
  StdDev (1, PAR_dn,IEEE4,False)  
  
  Average (1, T_CR3000,IEEE4,False)
	StdDev (1, T_CR3000,IEEE4,False)
  Average (1, V_CR3000,IEEE4,False)
	Minimum (1, V_CR3000,IEEE4,False,False)
	
EndTable

'Raw Summary Met Data
DataTable (Met30Min_raw,TRUE,-1)
	DataInterval (0,30,Min,100)
  TableFile ("CRD:"&Status.SerialNumber(1,1)&".CP_ECF_Met30Min_raw",64,-1,0,1,Day,0,0)
  
	Average (5,CNR4_raw,IEEE4,0)
	FieldNames("Avrg_SWD_raw, Avrg_SWU_raw, Avrg_LWD_raw, Avrg_LWU_raw, Avrg_Tb_raw")
	StdDev (5,CNR4,IEEE4,False)
	FieldNames("STDev_SWD_raw, STDev_SWU_raw, STDev_LWD_raw, STDev_LWU_raw, STDev_Tb_raw")
	
	Average (3,AirT_raw,IEEE4,0)
	FieldNames("Avrg_AirT_2m_raw, Avrg_AirT_4m_raw, Avrg_AirT_6m_raw")
	StdDev (3,AirT_raw,IEEE4,0)
	FieldNames("STDev_AirT_2m_raw, STDev_AirT_4m_raw, STDev_AirT_6m_raw")
	
	Average (1,TTmV,IEEE4,0)
  FieldNames("Avrg_SurfaceT_raw")
	StdDev (1,TTmV,IEEE4,0)
  FieldNames("STDev_SurfaceT_raw")
  
	Average (2,PAR_raw,IEEE4,0)
  FieldNames("Avrg_PARup_raw, Avrg_PARdn_raw")
	StdDev (2,PAR_raw,IEEE4,0)
  FieldNames("STDev_PARup_raw, STDev_PARdn_raw")
  
EndTable

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'Program
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
BeginProg
  
	SDMSpeed (40)
  Scan (100,mSec,1200,0)

  'Measure CSAT3A and CSAT3B
  CSAT3 (Sonic_CSAT3B,1,3,91,10)

	'Measure LI-7500 IRGA
	CS7500 (rho_c_LI7500,1,7,6)
  rho_c_LI7500 = rho_c_LI7500*44.0095
  rho_v_LI7500 = rho_v_LI7500*18.0153*0.001

  'RM Young 05103 Wind Speed & Direction measurements
	PulseCount(WS(1),1,1,1,1,0.098,0)
	BrHalf(WD(1),1,mV5000,12,2,1,5000,True,0,250,355,0)
	If WD(1) >= 360 OR WD(1) < 0 Then WD(1) = 0
	PulseCount(WS(2),1,2,1,1,0.098,0)
	BrHalf(WD(2),1,mV5000,21,2,1,5000,True,0,250,355,0)
	If WD(2) >= 360 OR WD(2) < 0 Then WD(2) = 0

  'Output
  CallTable TimeSeriesData
  
NextScan

SlowSequence
		
  Scan (5,Sec,3,0)
  'Measure CR3000 Diagnostics
  PanelTemp (T_CR3000,250)
  Battery (V_CR3000)

	'Measure CNR4
  VoltDiff (CNR4(),4,mV20,1,True ,0,250,1.0,0)
  BrHalf(CNR4(5),1,mV5000,9,1,1,2500,True,0,250,1,0)
'  BrHalf4W (CNR4(5),1,mV1000,mV1000,9,Vx1,1,1000,True ,True ,0,250,100,0)
  Move (CNR4_raw,5,CNR4,5)
	Tb = 1000*(CNR4(5)/(1-CNR4(5)))
	Tb = 1/(1.0295e-3+2.391e-4*LN(CNR4(5))+1.568e-7*(LN(CNR4(5)))^3)-273.15
  SWD = CNR4(1)/pyrano_upper*1000 'calculate short wave down 
  SWU = CNR4(2)/pyrano_lower*1000 'calculate short wave up
  LWD = CNR4(3)/pyrgeo_upper*1000 + 5.67e-8*(Tb+273.15)^4 'calculate long wave down
  LWU = CNR4(4)/pyrgeo_lower*1000 + 5.67e-8*(Tb+273.15)^4 'calculate long wave up
  Rn = SWU - SWD + LWU - LWD
  albedo = SWD/SWU
  
  'Measure HMP155 @6m
  VoltSe(AirT_6m,1,mV1000,10,0,0,250,1,0)
	Move(AirT_raw(3),1,AirT_6m,1) 'store raw data
  AirT_6m = AirT_6m * 0.14 -80 'convert to Temp (C)
	VoltSe(RH_6m,1,mV1000,11,0,0,250,0.1,0)
	If RH_6m > 100 AND RH_6m < 108 Then RH_6m = 100
	
	'Measure RTD Temperature Probe @2m
	BrHalf4W(AirT_2m,1,mV1000,mV1000,7,2,1,5000,True,True,0,250,1,0)
	Move(AirT_raw(1),1,AirT_2m,1) 'store raw data
	PRT(AirT_2m,1,AirT_2m,1.0267,0) 'convert to Temp (C)
	'Measure RTD Temperature Probe @4m
	BrHalf4W(AirT_4m,1,mV1000,mV1000,9,2,1,5000,True,True,0,250,1,0)
	Move(AirT_raw(2),1,AirT_4m,1) 'store raw data
	PRT(AirT_4m,1,AirT_4m,1.0267,0) 'convert to Temp (C)
	
	'LI190SB Quantum Sensor measurements
	VoltDiff(PAR(1),2,mV20,13,True,0,250,1,0)
	Move(PAR_raw(1),2,PAR(1),2)
	If PAR(1) < 0 Then PAR(1) = 0
	If PAR(2) < 0 Then PAR(2) = 0
	PAR(1) = PAR(1)* 1000/LI190_Cal_1*0.604 'calculate flux density
	PAR(2) = PAR(2)* 1000/LI190_Cal_2*0.604 'calculate flux density

	'SI-111 Precision Infrared Radiometer measurements of surface temp
	'Measure SI-111 sensor body thermistor temperature
	Therm109(SBT_C,1,22,3,0,250,1,0)
	'Measure SI-111 output of thermopile
	VoltDiff(TTmV,1,mV20,12,True,0,250,1,0,)
	'Calculate slope (m) and offset (b) coefficients for target temperature calculation
	m = const_M1 + (const_M2*SBT_C) + (const_M3*SBT_C^2)
	b = const_B1 + (const_B2*SBT_C) + (const_B3*SBT_C^2)
	'Calculate target temperature using calculated slope (m) and offset (b)
	SBT_K = SBT_C + 273.15
	TT_K = SBT_K^4 + TTmV*m + b
	TT_K = SQR(SQR(TT_K))
	'Convert target temperature into desired units
	SurfT = TT_K - 273.15

  'Output
	CallTable Flux30Min
  CallTable Met30Min
  CallTable Met30Min_raw  

NextScan
EndProg
