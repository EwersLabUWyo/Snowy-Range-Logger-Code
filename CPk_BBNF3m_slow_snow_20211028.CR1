'CR1000 Series
'Created by Short Cut (4.0)

'Wiring diagram:
'Wire            | Purpose           | Port
'---------------------------------------------
'SR50A Clr       | Shield            | G
'SR50A Blk       | G                 | G
'SR50A Red       | Power +           | +12V
'SR50A Grn       | On/Off            | C1
'SR50A Wht       | ????????          | G
'                |                   |
'MUXRef Red      | Signal -          | 2L 
'MUXRef Blu      | Signal +          | 2H
'AirT Red        | Signal -          | 1L
'AirT Blu        | Signal +          | 1H
'                |                   |
'AM16/32 12V     | Power +           | +12V
'AM16/32 G       | Ground            | G
'AM16/32 Clk     | Clock             | C2
'AM16/32 Res     | On/Off            | C3
'AM16/32 COMODD H| Signal +          | 3H
'AM16/32 COMODD L| Signal -          | 3L
'AM16/32 COM GND | Jumper to Ground  | AM16/32 G
'                |                   |
'Pole A Blues    | Signal +          | AM16/32 (1-9)H
'Pole A Reds     | Signal -          | AM16/32 (1-9)L
'Pole B Blues    | Signal +          | AM16/32 (10-18)L
'Pole B Reds     | Signal -          | AM16/32 (10-18)L
'Birba Blues     | Signal +          | AM16/32 (19-22)H
'Birba Reds      | Signal -          | AM16/32 (19-22)L

'Declare Variables and Units
'Default variables
Dim LCount
Public BattV
Public PTemp_C
Units BattV=Volts
Units PTemp_C=Deg C

'Snow depth sensor
Const Height=2.05 'instrument height, m
Public SR50A(2)
Public TempCompDistTo
Public Depth
Alias SR50A(1)=DistTo
Alias SR50A(2)=Qual
Units Depth=m
Units TempCompDistTo=m

'Thermocouples
Public MUXTemp 'reference temp for thermocouples
Public PingerAirtemp 'airtemp for snow pinger
Public Temp_C(32) 'birba and snowpack thermocouples
Alias Temp_C(1)=PoleA(15)
Alias Temp_C(16)=PoleB(13)
Alias Temp_C(29)=Birba(4)
Units MUXTemp=Deg C
Units Temp_C=Deg C
Units PingerAirtemp=Deg C

'Define Data Tables
DataTable(Snowpack,True,-1)
	DataInterval(0,30,Min,10)
	'The following TableFile instruction causes new data to be copied to an optional SC115 Flash
	'Memory Drive the moment it is plugged into the CS I/O port of the datalogger.
	'This mode of operation is referred to as Data Collection Mode (see the SC115 manual).
	'This TableFile instruction has no effect if an SC115 is never plugged into the datalogger.
	TableFile("USB:"+Status.SerialNumber+"Data",8,-1,0,0,Hr,0,0)
	
  'snow pinger variables
	Average(1,DistTo,FP2,False)
	Average(1,Qual,FP2,False)
	Average(1,TempCompDistTo,FP2,False)
	Average(1,Depth,FP2,False)
	Average(1,PingerAirtemp,FP2,False)
	Maximum(1,DistTo,FP2,False,False)
	Maximum(1,Qual,FP2,False,False)
	Maximum(1,TempCompDistTo,FP2,False,False)
	Maximum(1,Depth,FP2,False,False)
	Maximum(1,PingerAirtemp,FP2,False,False)
	Minimum(1,DistTo,FP2,False,False)
	Minimum(1,Qual,FP2,False,False)
	Minimum(1,TempCompDistTo,FP2,False,False)
	Minimum(1,Depth,FP2,False,False)
	Minimum(1,PingerAirtemp,FP2,False,False)
	
  'MUX thermocouple reference temp
	Average(1,MUXTemp,FP2,False)
	Maximum(1,MUXTemp,FP2,False,False)
	Minimum(1,MUXTemp,FP2,False,False)
	
  'Snowpack thermocouples (pole A)
  Average(13,PoleA(),FP2,False)
  Average(13,PoleB(),FP2,False)
  Average(4,Birba(),FP2,False)
  Maximum(13,PoleA(),FP2,False,False)
  Maximum(13,PoleB(),FP2,False,False)
  Maximum(4,Birba(),FP2,False,False)
  Minimum(13,PoleA(),FP2,False,False)
  Minimum(13,PoleB(),FP2,False,False)
  Minimum(4,Birba(),FP2,False,False)
  StdDev(13,PoleA(),FP2,False)
  StdDev(13,PoleB(),FP2,False)
  StdDev(4,Birba(),FP2,False)
  
EndTable

DataTable(Table2,True,-1)
	DataInterval(0,30,Min,10)
	'The following TableFile instruction causes new data to be copied to an optional SC115 Flash
	'Memory Drive the moment it is plugged into the CS I/O port of the datalogger.
	'This mode of operation is referred to as Data Collection Mode (see the SC115 manual).
	'This TableFile instruction has no effect if an SC115 is never plugged into the datalogger.
	TableFile("USB:"+Status.SerialNumber+"Table2",8,-1,0,0,Hr,0,0)
	
	Minimum(1,BattV,FP2,False,False)
EndTable

'Main Program
BeginProg
	'Main Scan
	Scan(60,Sec,1,0)
		Battery(BattV)
		PanelTemp(PTemp_C,60)
		
		'SR50A Sonic Ranging Sensor (SDI-12 Output) 
		'measurements 'DistTo' and 'Qual'
		'calculations 'TempCompDistTo' and 'Depth'
		SDI12Recorder(SR50A(),1,"0","M1!",1,0,-1)
		TCDiff(PingerAirtemp,1,mV2_5C,1,TypeT,PTemp_C,True,0,60,1,0)
		TempCompDistTo=DistTo*SQR((PingerAirtemp+273.15)/273.15)
		Depth=Height-TempCompDistTo
		
    'Type T Thermocouple measurements 'MUXTemp' for snopack and birba reference
		TCDiff(MUXTemp,1,mV2_5C,2,TypeT,PTemp_C,True,0,60,1,0)
		
		'Turn AM16/32 Multiplexer On
		PortSet(3,1)
		Delay(0,150,mSec)
		LCount=1
		SubScan(0,uSec,32)
			'Switch to next AM16/32 Multiplexer channel
			PulsePort(2,10000)
			
			'Type T Thermocouple measurements 'Temp_C()' on the AM16/32 Multiplexer
			TCDiff(Temp_C(LCount),1,mV2_5C,3,TypeT,MUXTemp,True,0,60,1,0)
			
			LCount=LCount+1
		NextSubScan
		'Turn AM16/32 Multiplexer Off
		PortSet(3,0)
		Delay(0,150,mSec)
		
		'Call Data Tables and Store Data
		CallTable Snowpack
		CallTable Table2
	NextScan
EndProg
