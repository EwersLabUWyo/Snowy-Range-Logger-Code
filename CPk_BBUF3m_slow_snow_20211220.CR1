'CR1000 Series
'Measure many snow depth snow temperature profile
'Author: Alex Fox
'Edited 20220527: Added author
'
'Wiring diagram:
'Wire            | Purpose           | Port
'---------------------------------------------
'JuddComm Clr    | Shield            | G
'JuddComm Blk    | Power -           | G
'JuddComm Red    | Power +           | +12V
'JuddComm Grn    | On/Off            | C1
'JuddComm Wht    | Signal +          | 1H
'JuddComm Brn    | Signal -          | 1L
'                |                   |
'MUXRef Red      | Signal -          | 3L 
'MUXRef Blu      | Signal +          | 3H
'                |                   |
'AM16/32 12V     | Power +           | +12V
'AM16/32 G       | Ground            | G
'AM16/32 Clk     | Clock             | C2
'AM16/32 Res     | On/Off            | C3
'AM16/32 COMODD H| Signal +          | 2H
'AM16/32 COMODD L| Signal -          | 2L
'AM16/32 COM GND | Jumper to Ground  | AM16/32 G
'                |                   |
'Pole A Blues    | Signal +          | AM16/32 (1-13)H
'Pole A Reds     | Signal -          | AM16/32 (1-13)L
'Pole B Blues    | Signal +          | AM16/32 (16-28)L
'Pole B Reds     | Signal -          | AM16/32 (16-28)L

'\\\\\\\\\\\\\\ DECLARATIONS /////////////////
'Default vars
Dim LCount
Public BattV
Public PTemp_C
Units BattV=Volts
Units PTemp_C=Deg C

'Snow depth sensor
Public AirT_C
Public Depth
Public Dist
Const Height = 1.9
Units AirT_C = Deg C
Units Depth = m
Units Dist = m

'Thermocouples
Public MUXTemp 'reference temp for thermocouples
Public Temp_C(30) 'TCs plugged into AM16/32Alias Temp_C(1)=PoleA(9)
Alias Temp_C(1)=PoleA(15) 'snow temp profile
Alias Temp_C(16)=PoleB(15) 'snow temp profile
Units MUXTemp=Deg C 
Units Temp_C()=Deg C

'\\\\\\\\\\\\\\\\\\ OUTPUT SECTION ///////////////////
'Define Data Tables
DataTable(Snowpack,True,-1)
	DataInterval(0,30,Min,10)
	'The following TableFile instruction causes new data to be copied to an optional SC115 Flash
	'Memory Drive the moment it is plugged into the CS I/O port of the datalogger.
	'This mode of operation is referred to as Data Collection Mode (see the SC115 manual).
	'This TableFile instruction has no effect if an SC115 is never plugged into the datalogger.
	TableFile("USB:"+Status.SerialNumber+"Snowpack_BBUF",8,-1,0,0,Hr,0,0)
	
  'snow pinger variables
  Average(1,Depth,FP2,False)
	Maximum(1,Depth,FP2,False,False)
	Minimum(1,Depth,FP2,False,False)
	
  Average(1,Dist,FP2,False)
	Maximum(1,Dist,FP2,False,False)
	Minimum(1,Dist,FP2,False,False)
	
  Average(1,AirT_C,FP2,False)
	Maximum(1,AirT_C,FP2,False,False)
	Minimum(1,AirT_C,FP2,False,False)
	
  'MUX thermocouple reference temp
	Average(1,MUXTemp,FP2,False)
	Maximum(1,MUXTemp,FP2,False,False)
	Minimum(1,MUXTemp,FP2,False,False)
	
  'Snowpack thermocouples (pole A)
  'only store first 13 values, since only those are connected to anything
  Average(13,PoleA(),FP2,False)
  Average(13,PoleB(),FP2,False)
  Maximum(13,PoleA(),FP2,False,False)
  Maximum(13,PoleB(),FP2,False,False)
  Minimum(13,PoleA(),FP2,False,False)
  Minimum(13,PoleB(),FP2,False,False)
  StdDev(13,PoleA(),FP2,False)
  StdDev(13,PoleB(),FP2,False)
  
EndTable

DataTable(Table2,True,-1)
	DataInterval(0,30,Min,10)
	'The following TableFile instruction causes new data to be copied to an optional SC115 Flash
	'Memory Drive the moment it is plugged into the CS I/O port of the datalogger.
	'This mode of operation is referred to as Data Collection Mode (see the SC115 manual).
	'This TableFile instruction has no effect if an SC115 is never plugged into the datalogger.
	TableFile("USB:"+Status.SerialNumber+"Table2",8,-1,0,0,Hr,0,0)
	
	Minimum(1,BattV,FP2,False,False)
EndTable

'\\\\\\\\\\\\\\\\\\\\ MAIN PROGRAM ////////////////////////
'Main Program
BeginProg
	'Main Scan
	Scan(60,Sec,1,0)
	  '---------------------------------------------
	  ' Get and process inputs
	  '---------------------------------------------
		Battery(BattV)
		PanelTemp(PTemp_C,60)
		
		'JuddComm Sonic Ranging Sensor
    PortSet(1, 1)
    'wait 0.8 seconds for the sensor to output the Air Temp.
    Delay(0,800,mSec)
    VoltDiff(AirT_C, 1, mV5000, 1, True, 0, 60, 0.2, -273) '0.2 K/mV
    'wait another 1.8 seconds to compute temp-corrected depth
    Delay(0,1800,MSEC)
    VoltDiff(Dist, 1, mV5000, 1, True, 0, 60, 0.005, 0) '0.005 m/mV
    Depth = Height - Dist
    PortSet(1, 0)
    
    'Reference temp for thermocouples plugged into MUX
    TCDiff(MUXTemp, 1,mV2_5C, 3,TypeT,PTemp_C,True,0,60,1,0)
    
    'Thermocouples on MUX	
		'Turn AM16/32 Multiplexer On
		PortSet(3,1)
		Delay(0,150,mSec)
		LCount=1
		SubScan(0,uSec,30)
			'Switch to next AM16/32 Multiplexer channel
			PulsePort(2,10000)
			'Type T Thermocouple measurements 'Temp_C()' on the AM16/32 Multiplexer
			TCDiff(Temp_C(LCount),1,mV2_5,2,TypeT,MUXTemp,True,0,60,1,0)
			LCount=LCount+1
		NextSubScan
		'Turn AM16/32 Multiplexer Off
		PortSet(3,0)
		Delay(0,150,mSec)
		
		'--------------------------------------------
		'Store Data
		'---------------------------------------------
		CallTable Snowpack
		CallTable Table2
	NextScan
EndProg
